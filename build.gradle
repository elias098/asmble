buildscript {
    ext.kotlin_version = '1.9.0'
    ext.asm_version = '6.2.1'

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.5'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version" apply false
}

allprojects {
    group 'com.github.elias098.asmble'
    version = System.getenv("RELEASE_VERSION") ?: "NO-GIT-TAG-SET"

    apply plugin: 'java-library'
    group 'com.github.elias098.asmble'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    repositories {
        mavenCentral()
    }
}

project(':annotations') {
    javadoc {
        options.links 'https://docs.oracle.com/javase/8/docs/api/'
        // TODO: change when https://github.com/gradle/gradle/issues/2354 is fixed
        options.addStringOption 'Xdoclint:all', '-Xdoclint:-missing'
    }

    publishSettings(project, 'asmble-annotations', 'Asmble WASM Annotations', true)
}

project(':compiler') {
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: 'application'
    
    applicationName = "asmble"
    mainClassName = "asmble.cli.MainKt"

    dependencies {
        api project(':annotations')
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        implementation "org.ow2.asm:asm-tree:$asm_version"
        implementation "org.ow2.asm:asm-util:$asm_version"
        implementation "org.ow2.asm:asm-commons:$asm_version"
        testImplementation 'junit:junit:4.12'
        testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    }

    publishSettings(project, 'asmble-compiler', 'Asmble WASM Compiler', false)
}

def publishSettings(project, projectName, projectDescription, includeJavadoc) {
    project.with {
        apply plugin: 'maven-publish'

        java {
//            withJavadocJar()
            withSourcesJar()
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = projectName
                    from components.java

                    versionMapping {
                        usage('java-api') {
                            fromResolutionOf('runtimeClasspath')
                        }
                        usage('java-runtime') {
                            fromResolutionResult()
                        }
                    }

                    pom {
                        name = projectName
                        description = projectDescription
                        url = 'https://github.com/elias098/asmble'
                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'https://opensource.org/licenses/MIT'
                            }
                        }
                        developers {
                            developer {
                                id = 'elias098'
                                url = 'https://github.com/elias098'
                            }
                        }
                    }
                }
            }
            repositories {
                repositories {
                    maven {
                        name = "GitHubPackages"
                        url = "https://maven.pkg.github.com/elias098/asmble"
                        credentials {
                            username = System.getenv("GITHUB_ACTOR")
                            password = System.getenv("GITHUB_TOKEN")
                        }
                    }
                }
            }
        }
    }
}
